import com.install4j.gradle.Install4jTask
import de.undercouch.gradle.tasks.download.Download
import org.gradle.internal.os.OperatingSystem

buildscript {
  repositories {
    jcenter()
    maven { url 'https://maven.ej-technologies.com/repository' }
    maven { url 'https://repo.spring.io/plugins-release' }
  }

  dependencies {
    classpath("org.springframework.build.gradle:propdeps-plugin:${propdepsVersion}")
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

plugins {
  id "com.install4j.gradle" version "8.0.7"
  id "co.riiid.gradle" version "0.4.2"
  id "de.undercouch.download" version "3.4.3"
  id "io.freefair.lombok" version "5.1.0"
  id 'jacoco'
  id 'com.github.kt3k.coveralls' version "${coverallsGradlePluginVersion}"
  id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
  id 'org.beryx.runtime' version '1.11.0'
}

apply from: "${rootProject.rootDir}/gradle/update4j.gradle"
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

mainClassName = "com.faforever.client.Main"
group 'com.faforever'

// These are read from gradle.properties
version "${version}"
println "Version is ${version}"
project.ext.javafxPlatform = "${javafxPlatform}"

bootJar.enabled = false
jar.enabled = true

if (javafxPlatform == "unspecified") {
  switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
      project.ext.javafxPlatform = "linux"
      break
    case OperatingSystem.MAC_OS:
      project.ext.javafxPlatform = "mac"
      break
    case OperatingSystem.WINDOWS:
      project.ext.javafxPlatform = "win"
      break
  }
}
println "Platform is: ${javafxPlatform}"

compileJava.dependsOn(processResources)
processResources {
  filesMatching('**/application.yml') {
    filter {
      it.replace('#faf-client.version#', project.version.toString())
    }
  }
}

configurations {
  compile.exclude module: "assertj-core"
}

jacocoTestReport {
  reports {
    xml.enabled = true
  }
}

test {
  testLogging {
    showStandardStreams = true
  }
}

configurations {
  codacy
  webViewPatch
  updater
}

jar {
  doFirst {
    manifest {
      attributes "Main-Class": project.mainClassName,
          "Implementation-Title": "FAF Client",
          "Implementation-Version": version,
          "Implementation-Vendor": "Downlord",
          "Class-Path": (configurations.runtimeClasspath.collect { it.name }.join(' '))
    }
  }
}

runtime {
  options = ['--compress', '2', '--no-header-files', '--no-man-pages', '--vm=client']
  modules = [
      'java.desktop',
      'jdk.jsobject',
      'java.xml',
      'java.logging',
      'jdk.unsupported',
      'java.datatransfer',
      'java.scripting',
      // We don't actually need this, but at least Gson's type factories depend on it
      'java.sql',
      'java.naming',
      'java.net.http',
      'jdk.xml.dom',
      // Required for TLS
      'jdk.crypto.ec',
      'java.instrument',
      // Remote debugging
      'jdk.jdwp.agent',
      // JMX, allows us to get the PID
      'java.management'
  ]
  jpackage {
    // Can't rename these yet: https://github.com/beryx/badass-runtime-plugin/issues/62
    imageName = project.name
    applicationName = "FAF Client"
    imageOptions = [
        '--icon', "${rootDir}/src/media/appicon/faf.ico"
    ]
    jvmArgs = [
        '-splash:app/splash.png',
        '-DexternalToolsDir=app',
        '-Dprism.dirtyopts=false',
        '-Xms128m',
        '-Xmx712m',
        '-XX:MinHeapFreeRatio=15',
        '-XX:MaxHeapFreeRatio=33',
        '-XX:+HeapDumpOnOutOfMemoryError',
        '-XX:+UseStringDeduplication',
        '-javaagent:app/webview-patch.jar',
        '-Djava.net.preferIPv4Stack=true'
    ]
  }
}

task jpackageImageZip(type: Zip, dependsOn: jpackageImage) {
  group 'build'
  from jpackageImage.outputs.files.singleFile.toString() + "/" + project.name
  into project.version
  destinationDirectory = file("$buildDir/dist")
}

distributions {
  main {
    contents {
      into('lib') {
        from { [downloadIceAdapter.outputs.files, downloadUnixUid.outputs.files, downloadWindowsUid.outputs.files] }
        from "${rootDir}/src/media/splash/splash.png"

        from(configurations.webViewPatch) {
          include "*webview-patch*.jar"
          rename '.*webview-patch.*\\.jar', 'webview-patch.jar'
        }
        from(configurations.updater) {
          include "updater-*.jar"
          rename 'updater-.*\\.jar', 'updater.jar'
        }
      }
      exclude('**/i4jruntime*.jar')
      into('')
    }
  }
}

install4j {
  if (project.hasProperty('install4jHomeDir')) {
    installDir = file(install4jHomeDir)
  }
  if (project.hasProperty('install4jLicense')) {
    license = install4jLicense
  }
}

task buildInstall4jMediaFiles(type: Install4jTask) {
  group "distribution"
  description 'Creates install4j media files'
  dependsOn 'installDist'

  projectFile = file('downlords-faf-client.install4j')
  release = version

  switch (project.ext.javafxPlatform) {
    case "linux":
      mediaTypes = ['unixInstaller', 'unixArchive']
      break
    case "mac":
      mediaTypes = ['macos', 'macosFolder', 'macosArchive', 'macosFolderArchive']
      break
    case "win":
      mediaTypes = ['windows', 'windowsArchive']
      break
  }

  doFirst {
    println "Building version ${version}"
    if (!project.hasProperty('install4jHomeDir')) {
      throw new RuntimeException('Specify install4jHomeDir in gradle.properties and set it to an install4j installation directory')
    }
  }
}

task downloadWebViewPatch(type: Copy) {
  from(configurations.webViewPatch) {
    include "*webview-patch*.jar"
  }
  into "${buildDir}/webview-patch/"
  rename '.*webview-patch.*\\.jar', 'webview-patch.jar'
}
assemble.dependsOn downloadWebViewPatch

/* Makes updater.jar available as a resource to the application. */
task copyUpdaterToResources(type: Copy) {
  from(configurations.updater) {
    include "updater-*.jar"
  }
  into "${buildDir}/resources/updater"
  rename 'updater-.*\\.jar', 'updater.jar'
}
assemble.dependsOn copyUpdaterToResources

task downloadIceAdapter(type: Download) {
  group "external tools"
  src "https://github.com/FAForever/java-ice-adapter/releases/download/v${faf_ice_adapter_version}/faf-ice-adapter.jar"
  dest file("${buildDir}/resources/tools/faf-ice-adapter.jar")
  onlyIfNewer true
}

task downloadUnixUid(type: Download) {
  group "external tools"
  src "https://github.com/FAForever/uid/releases/download/v${faf_uid_version}/faf-uid"
  dest file("${buildDir}/resources/tools/faf-uid")
  onlyIfNewer true
  doLast {
    file("${buildDir}/resources/tools/faf-uid").setExecutable(true, false)
  }
}

task downloadWindowsUid(type: Download) {
  group "external tools"
  src "https://github.com/FAForever/uid/releases/download/v${faf_uid_version}/faf-uid.exe"
  dest file("${buildDir}/resources/tools/faf-uid.exe")
  onlyIfNewer true
}

task downloadNativeDependencies(dependsOn: [downloadUnixUid, downloadWindowsUid, downloadIceAdapter]) {
  group "external tools"
}

assemble.dependsOn 'downloadNativeDependencies'
createUpdate4jConfig.dependsOn jpackageImage

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}

idea.project.settings.taskTriggers {
  beforeSync tasks.getByName("downloadNativeDependencies"), tasks.getByName("copyUpdaterToResources")
}

repositories {
  jcenter()
  maven { url "https://jitpack.io" }
  maven { url 'https://maven.ej-technologies.com/repository' }
  maven { url "https://dl.bintray.com/typesafe/maven-releases" }
  maven { url "https://maven.teamdev.com/repository/products" }
  maven { url "https://repo.spring.io/milestone/" }
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter")
  compile("org.springframework.boot:spring-boot-starter-cache")
  compile("org.springframework.boot:spring-boot-starter-logging")
  compile("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${springSecurityOauth2AutoconfigureVersion}")
  compile("org.springframework:spring-web")

  compile("com.github.FAForever:faf-java-commons:${fafCommonsVersion}") {
    exclude module: 'luaj-jse'
    exclude module: 'guava'
  }
  compile("com.google.code.gson:gson:2.8.5")
  compile("com.github.micheljung:pircbotx:18481dcbfd") {
    exclude module: 'guava'
  }
  compile("com.google.guava:guava:28.0-jre")
  compile("org.apache.commons:commons-compress:1.18")
  compile("net.java.dev.jna:jna:${jnaVersion}")
  compile("net.java.dev.jna:jna-platform:${jnaVersion}")
  compile("org.eclipse.jgit:org.eclipse.jgit:5.1.3.201810200350-r")
  compile("org.jetbrains:annotations:16.0.3")
  compile("net.sf.jtidy:jtidy:r938")
  compile("com.neovisionaries:nv-i18n:1.23")
  compile("com.rometools:rome:1.11.1")
  compile("org.jsoup:jsoup:1.11.3")
  compile("com.nativelibs4java:bridj:0.7.0")
  compile("com.github.nocatch:nocatch:1.1")
  // Custom version because of https://github.com/luaj/luaj/issues/70
  compile("com.github.micheljung:luaj:da919e2")
  compile("commons-validator:commons-validator:1.6")
  compile("com.github.micheljung:JJsonRpc:01a7fba5f4")
  compile("javax.inject:javax.inject:1")
  compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeJsr310Version}")
  compile("com.github.rutledgepaulv:q-builders:${qBuildersVersion}")
  compile("com.github.nsp:JSkills:${jSkillsVersion}")
  compile("com.github.jasminb:jsonapi-converter:${jsonapiConverterVersion}")
  compile("com.jfoenix:jfoenix:${jfoenixVersion}")
  compile("javax.annotation:javax.annotation-api:1.3.1")
  compile("com.github.ben-manes.caffeine:caffeine")
  compile("org.apache.httpcomponents:httpclient")
  compile("ch.micheljung.fxstage:fxstage:0.7.2")
  compile("ch.micheljung.waitomo:jfx-waitomo-theme:0.2.0")
  compile "org.update4j:update4j:${update4jVersion}"

  compile("org.openjfx:javafx-base:${javafxVersion}:${javafxPlatform}")
  compile("org.openjfx:javafx-controls:${javafxVersion}:${javafxPlatform}")
  compile("org.openjfx:javafx-graphics:${javafxVersion}:${javafxPlatform}")
  compile("org.openjfx:javafx-swing:${javafxVersion}:${javafxPlatform}")
  compile("org.openjfx:javafx-fxml:${javafxVersion}:${javafxPlatform}")
  compile("org.openjfx:javafx-web:${javafxVersion}:${javafxPlatform}")
  compile("org.openjfx:javafx-media:${javafxVersion}:${javafxPlatform}")
  compile("com.github.axel1200:discord-rpc:${discordRpcVersion}")

  webViewPatch('com.github.micheljung:javafx-transparent-webview-patch:0468c80')
  updater project(':updater')
  compile("org.javassist:javassist:${project.ext['javassist.version']}")

  compileOnly("com.install4j:install4j-runtime:${install4jRuntimeVersion}")

  testCompile 'junit:junit:4.13'
  testCompile("org.testfx:testfx-core:4.0.8-alpha") {
    exclude module: 'guava'
  }
  testCompile("org.testfx:testfx-junit:4.0.8-alpha")
  testCompile("com.natpryce.hamcrest:hamcrest-reflection:0.1-2")
  testCompile("org.springframework.boot:spring-boot-starter-test")

  annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

  codacy("com.github.codacy:codacy-coverage-reporter:-SNAPSHOT")
}

task downloadWebViewPatch{

}