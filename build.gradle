import com.install4j.gradle.Install4jTask
import org.gradle.internal.os.OperatingSystem

buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/plugins-release' }
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

plugins {
  id "com.install4j.gradle" version "${install4jVersion}"
  id "co.riiid.gradle" version "0.4.2"
  id "de.undercouch.download" version "4.1.1"
  id 'jacoco'
}

// source: https://github.com/mendhak/Gradle-Travis-Colored-Output/blob/master/ColoredOutput.gradle
tasks.withType(Test) {

  String ANSI_BOLD_WHITE = "\u001B[0;1m";
  String ANSI_RESET = "\u001B[0m";
  String ANSI_BLACK = "\u001B[30m";
  String ANSI_RED = "\u001B[31m";
  String ANSI_GREEN = "\u001B[32m";
  String ANSI_YELLOW = "\u001B[33m";
  String ANSI_BLUE = "\u001B[34m";
  String ANSI_PURPLE = "\u001B[35m";
  String ANSI_CYAN = "\u001B[36m";
  String ANSI_WHITE = "\u001B[37m";
  String CHECK_MARK = "\u2713";
  String NEUTRAL_FACE = "\u0CA0_\u0CA0";
  String X_MARK = "\u274C";
  String indent = '    ';

  afterTest { descriptor, result ->
    if (result.failedTestCount > 0) {
      String exceptions = result.getException().toString().split("\\n").join("\n" + indent)

      out.println("\n" + ANSI_BOLD_WHITE + descriptor.parent.name + ANSI_RESET
                  + "\n" + indent + ANSI_RED + X_MARK + ANSI_RESET + " " + descriptor.name
                  + "\n" + indent + exceptions)
    }
  }

  afterSuite { desc, result ->
    if (!desc.parent) { // will match the outermost suite
      def failStyle = ANSI_RED
      def skipStyle = ANSI_YELLOW
      def summaryStyle = ANSI_WHITE

      switch (result.resultType) {
        case TestResult.ResultType.SUCCESS:
          summaryStyle = ANSI_GREEN;
          break;
        case TestResult.ResultType.FAILURE:
          summaryStyle = ANSI_RED;
          break;
      }

      out.println("--------------------------------------------------------------------------");
      out.println("Results: " + summaryStyle + "${result.resultType}" + ANSI_RESET
              + " (${result.testCount} tests, "
              + ANSI_GREEN + "${result.successfulTestCount} passed" + ANSI_RESET
              + ", " + failStyle + "${result.failedTestCount} failed" + ANSI_RESET
              + ", " + skipStyle + "${result.skippedTestCount} skipped" + ANSI_RESET
              + ")");
      out.println("--------------------------------------------------------------------------");
    }
  }

}

//Specify jacoco version to work with java15
jacoco {
  toolVersion = "0.8.6"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'distribution'


sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

// These are read from gradle.properties
version "${version}"
project.ext.javafxPlatform = "${javafxPlatform}"

bootJar.enabled = false
jar.enabled = true

if (javafxPlatform == "unspecified") {
  switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
      project.ext.javafxPlatform = "linux"
      break
    case OperatingSystem.MAC_OS:
      project.ext.javafxPlatform = "mac"
      break
    case OperatingSystem.WINDOWS:
      project.ext.javafxPlatform = "win"
      break
  }
}
println "Platform is: ${javafxPlatform}"

compileJava.dependsOn(processResources)
processResources {
  filesMatching('**/application.yml') {
    filter {
      it.replace('#faf-client.version#', project.version.toString())
    }
  }
}

configurations {
  compile.exclude module: "assertj-core"
}

jacocoTestReport {
  reports {
    xml.enabled = true
  }
}

test {
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
  systemProperties = [
          'junit.jupiter.execution.parallel.enabled': true
  ]
}

configurations {
  codacy
}


bootRun {
    main = "com.faforever.client.Main"
    jvmArgs = [
            "-DnativeDir=build/resources/native",
            "-Dprism.dirtyopts=false",
            "-Dprism.forceGPU=true",
            "-Xms128m",
            "-Xmx712m",
            "-XX:MinHeapFreeRatio=15",
            "-XX:MaxHeapFreeRatio=33",
            "-XX:+HeapDumpOnOutOfMemoryError",
            "-XX:+UseStringDeduplication",
            "-XX:ConcGCThreads=1",
            "-XX:ParallelGCThreads=1",
            "-XX:+UseG1GC",
            "-javaagent:webview-patch/build/libs/webview-patch.jar",
            "-Djava.net.preferIPv4Stack=true",
            "-XX:TieredStopAtLevel=1"
    ]
}

jar {
  manifest {
    attributes "Main-Class": "com.faforever.client.Main",
        "Implementation-Title": "FAF Client",
        "Implementation-Version": archiveVersion,
        "Implementation-Vendor": "Forged Alliance Forever"
  }
}

distributions {
  main {
    contents {
      from {
        [
            configurations.runtimeClasspath,
            jar,
            downloadIceAdapter.outputs.files,
            downloadUnixUid.outputs.files,
            downloadWindowsUid.outputs.files
        ]
      }
      exclude('**/i4jruntime*.jar')
    }
  }
}

install4j {
  if (project.hasProperty('install4jHomeDir')) {
    installDir = file(install4jHomeDir)
  }
  if (project.hasProperty('install4jLicense')) {
    license = install4jLicense
  }
}

task buildInstall4jMediaFiles(type: Install4jTask) {
  group "distribution"
  description 'Creates install4j media files'
  dependsOn 'installDist'

  projectFile = file('faf-client.install4j')
  release = version

  switch (project.ext.javafxPlatform) {
    case "linux":
      mediaTypes = ['unixInstaller', 'unixArchive']
      break
    case "mac":
      mediaTypes = ['macos', 'macosFolder', 'macosArchive', 'macosFolderArchive']
      break
    case "win":
      mediaTypes = ['windows', 'windowsArchive']
      break
  }

  doFirst {
    println "Building version ${version}"
    if (!project.hasProperty('install4jHomeDir')) {
      throw new RuntimeException('Specify install4jHomeDir in gradle.properties and set it to an install4j installation directory')
    }
  }
}

task downloadIceAdapter(type: Download) {
  group "native dependencies"
  src "https://github.com/FAForever/java-ice-adapter/releases/download/v${faf_ice_adapter_version}/faf-ice-adapter.jar"
  dest file("${buildDir}/resources/native/faf-ice-adapter.jar")
  onlyIfNewer true
}

task downloadUnixUid(type: Download) {
  group "native dependencies"
  src "https://github.com/FAForever/uid/releases/download/v${faf_uid_version}/faf-uid"
  dest file("${buildDir}/resources/native/faf-uid")
  onlyIfNewer true
  doLast {
    file("${buildDir}/resources/native/faf-uid").setExecutable(true, false)
  }
}

task downloadWindowsUid(type: Download) {
  group "native dependencies"
  src "https://github.com/FAForever/uid/releases/download/v${faf_uid_version}/faf-uid.exe"
  dest file("${buildDir}/resources/native/faf-uid.exe")
  onlyIfNewer true
}

task downloadNativeDependencies(dependsOn: [downloadUnixUid, downloadWindowsUid, downloadIceAdapter]) {
  group "native dependencies"
}

compileJava.dependsOn 'downloadNativeDependencies'
compileJava.dependsOn ':webview-patch:build'

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
  maven { url 'https://maven.ej-technologies.com/repository' }
}

dependencies {
  implementation("org.springframework.boot:spring-boot-starter")
  implementation("org.springframework.boot:spring-boot-starter-cache")
  implementation("org.springframework.boot:spring-boot-starter-logging")
  implementation("org.springframework.boot:spring-boot-starter-websocket")
  implementation("org.springframework.boot:spring-boot-starter-webflux")
  implementation("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${springSecurityOauth2AutoconfigureVersion}")
  implementation("org.springframework:spring-web")
  implementation("org.springframework:spring-websocket")

  implementation("com.github.FAForever:faf-java-commons:${fafCommonsVersion}") {
    exclude module: 'guava'
  }
  implementation("com.google.guava:guava:30.0-jre")
  implementation("org.apache.commons:commons-compress:1.20")
  implementation("net.java.dev.jna:jna:${jnaVersion}")
  implementation("net.java.dev.jna:jna-platform:${jnaVersion}")
  implementation("org.eclipse.jgit:org.eclipse.jgit:5.9.0.202009080501-r")
  implementation("org.jetbrains:annotations:20.1.0")
  implementation("com.neovisionaries:nv-i18n:1.23")
  implementation("com.rometools:rome:1.11.1")
  implementation("org.jsoup:jsoup:1.11.3")
  implementation("com.nativelibs4java:bridj:0.7.0")
  implementation("com.github.luben:zstd-jni:1.4.8-1")
  implementation("org.luaj:luaj-jse:3.0.1")
  implementation("commons-validator:commons-validator:1.7")
  implementation("com.github.micheljung:JJsonRpc:01a7fba5f4")
  implementation("javax.inject:javax.inject:1")
  implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
  implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
  implementation("com.github.rutledgepaulv:q-builders:${qBuildersVersion}")
  implementation("com.github.nsp:JSkills:${jSkillsVersion}")
  implementation("com.github.jasminb:jsonapi-converter:${jsonapiConverterVersion}")
  implementation("javax.annotation:javax.annotation-api:1.3.1")
  implementation("com.github.ben-manes.caffeine:caffeine")
  implementation("org.apache.httpcomponents:httpclient")
  implementation("io.github.micheljung:fxstage:0.8.0")
  implementation("io.github.micheljung:jfx-waitomo-theme:0.3.0")
  implementation("org.kitteh.irc:client-lib:7.3.0")
  implementation("org.mapstruct:mapstruct:${mapStructVersion}")

  implementation("org.openjfx:javafx-base:${javafxVersion}:${javafxPlatform}")
  implementation("org.openjfx:javafx-controls:${javafxVersion}:${javafxPlatform}")
  implementation("org.openjfx:javafx-graphics:${javafxVersion}:${javafxPlatform}")
  implementation("org.openjfx:javafx-swing:${javafxVersion}:${javafxPlatform}")
  implementation("org.openjfx:javafx-fxml:${javafxVersion}:${javafxPlatform}")
  implementation("org.openjfx:javafx-web:${javafxVersion}:${javafxPlatform}")
  implementation("org.openjfx:javafx-media:${javafxVersion}:${javafxPlatform}")
  implementation("com.github.1-alex98:discord-rpc:${discordRpcVersion}")
  implementation("org.controlsfx:controlsfx:${controlsfxVersion}")
  implementation("org.fxmisc.flowless:flowless:${flowlessVersion}")
  implementation("de.codecentric.centerdevice:javafxsvg:1.3.0")

  implementation(project(":webview-patch"))
  implementation("org.javassist:javassist:${project.ext['javassist.version']}")

  implementation("com.install4j:install4j-runtime:${install4jVersion}")

  testImplementation("com.squareup.okhttp3:okhttp")
  testImplementation("com.squareup.okhttp3:mockwebserver")
  testImplementation("io.projectreactor:reactor-test")
  testImplementation("org.testfx:testfx-core:4.0.16-alpha") {
    exclude module: 'guava'
  }
  testImplementation("io.projectreactor.netty:reactor-netty:1.0.4")
  testImplementation("org.testfx:testfx-junit5:4.0.16-alpha")
  testImplementation("com.natpryce.hamcrest:hamcrest-reflection:0.1-2")
  testImplementation("org.springframework.boot:spring-boot-starter-test")
  testImplementation("org.testfx:openjfx-monocle:jdk-12.0.1+2")

  annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
  annotationProcessor("org.projectlombok:lombok")
  annotationProcessor("org.mapstruct:mapstruct-processor:${mapStructVersion}")
  testAnnotationProcessor("org.projectlombok:lombok")
  testAnnotationProcessor("org.mapstruct:mapstruct-processor:${mapStructVersion}")
  compileOnly("org.projectlombok:lombok")
  testCompileOnly("org.projectlombok:lombok")

  codacy("com.github.codacy:codacy-coverage-reporter:-SNAPSHOT")
}
